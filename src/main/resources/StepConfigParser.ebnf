// This is a grammar for the step configuration file
//
// It is designed to be fed to http://www.bottlecaps.de/rex/

Steps          ::= Step* EOF

Step           ::= "step" StepName
                   Implementation*
                   Input*
                   Output*
                   Option*

Implementation ::= "has" "implementation" ClassName

Input          ::= "primary"? "input" "*"? PortName
Output         ::= "primary"? "output" "*"? PortName

Option         ::= "option" OptionName "?"? DeclaredType ("=" StringLiteral)?

DeclaredType   ::= TypeName | '(' Literal ('|' Literal)* ')'

// ...

Ignorable      ::= WhiteSpace | Comment /* ws: definition */

<?TOKENS?>

UnicodeInputCharacter  ::= UnicodeEscape | RawInputCharacter
UnicodeEscape          ::= '\' UnicodeMarker HexDigit HexDigit HexDigit HexDigit
UnicodeMarker          ::= 'u'+
RawInputCharacter      ::= [#x0001-#xD7FF] | [#xE000-#xFFFD] | [#x10000-#x10FFFF]
HexDigit               ::= [0-9a-fA-F]
InputCharacter         ::= UnicodeInputCharacter - ( CR | LF )

Comment                ::= TraditionalComment | EndOfLineComment
TraditionalComment     ::= '/*' ( UnicodeInputCharacter*
                                  - ( UnicodeInputCharacter* '*/' UnicodeInputCharacter* ) )
                           '*/'
EndOfLineComment       ::= '//' InputCharacter*

WhiteSpace             ::= ' ' | #x0009 | #x000C | LineTerminator

LF                     ::= #x000A
CR                     ::= #x000D
LineTerminator         ::= LF | CR | CR LF

StringLiteral          ::= '"' ( EscapeQuot | [^"] )* '"'
                         | "'" ( EscapeApos | [^'] )* "'"  /* ws: explicit */
EscapeQuot             ::= '""'
EscapeApos             ::= "''"

EOF                    ::= $

NameString             ::= NameStartChar NameChar*

PortName               ::= NameString
StepName               ::= NameString
OptionName             ::= NameString
ClassName              ::= NameString
TypeName               ::= NameString
Literal                ::= NameString

NameStartChar
         ::=
           | [A-Z]
           | '_'
           | [a-z]
           | [#x00C0-#x00D6]
           | [#x00D8-#x00F6]
           | [#x00F8-#x02FF]
           | [#x0370-#x037D]
           | [#x037F-#x1FFF]
           | [#x200C-#x200D]
           | [#x2070-#x218F]
           | [#x2C00-#x2FEF]
           | [#x3001-#xD7FF]
           | [#xF900-#xFDCF]
           | [#xFDF0-#xFFFD]
           | [#x10000-#xEFFFF]
NameChar ::= NameStartChar
           | '-'
           | '.'
           | [0-9]
           | #x00B7
           | [#x0300-#x036F]
           | [#x203F-#x2040]
